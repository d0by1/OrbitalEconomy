plugins {
    id 'java'
}

group 'me.d0by'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly ('org.spigotmc:spigot-api:1.13-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5', module: 'bungeecord-chat'
    }
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly ('com.github.MilkBowl:VaultAPI:1.7') {
        exclude group: 'org.bukkit', module: 'bukkit'
    }
    implementation 'com.zaxxer:HikariCP:5.0.1'

    // Annotations
    compileOnly 'org.jetbrains:annotations:23.0.0'
    annotationProcessor 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

sourceCompatibility = JavaVersion.VERSION_16.majorVersion
targetCompatibility = JavaVersion.VERSION_16.majorVersion

compileJava {
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: rootProject.version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

jar {
    dependsOn configurations.runtimeClasspath
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archivesBaseName = rootProject.name
    from {
        configurations.runtimeClasspath.findAll {
            it.name.endsWith('jar')
        }.collect {
            zipTree(it)
        }
    }
}
